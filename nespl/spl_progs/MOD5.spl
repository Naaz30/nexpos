//module 5
alias currentPID R0;
currentPID = [SYSTEM_STATUS_TABLE + 5*CORE + 1];

//The ExpL application does not push the Base Pointer register (BP)
multipush(BP);

alias processTableEntry R1;
processTableEntry = PROCESS_TABLE + 16*currentPID;

[processTableEntry + 12] = SP % PAGE_SIZE;		//kptr
[processTableEntry + 14] = PTBR;
[processTableEntry + 15] = PTLR;

R1 = ACQUIRE_SCHED_LOCK;
call ACCESS_CONTROL;

alias scheduleProc R1;

if(CORE == PRIMARY_CORE) then
	scheduleProc = IDLE_PROCESS;
	//if paging status is not 0 [paging is ongoing]
	if([SYSTEM_STATUS_TABLE + 5] != 0) then
		if([PROCESS_TABLE + 16*SWAPPER_DAEMON + 4] != WAIT_DISK) then		//not blocked by disk operation
			scheduleProc = SWAPPER_DAEMON;
		endif;
		goto schedule_proc;
	endif;

	//if logout status is 1 and secondary core is not running idle2, then schedule idle[wait for core 2 to schedule idle2]
	if([SYSTEM_STATUS_TABLE + 7] == 1 && [SYSTEM_STATUS_TABLE + 6] != IDLE2_PROCESS) then
		goto schedule_proc;
	endif;

	alias procId R2;
	procId = (currentPID+1) % MAX_PROC_NUM;
	alias procCount R3;
	procCount = 0;
	while (procCount < MAX_PROC_NUM) do

		R4 = [PROCESS_TABLE + 16*procId + 4];
		if (
			procId != IDLE_PROCESS && 
			procId != SWAPPER_DAEMON && 
			procId != IDLE2_PROCESS && 
			procId != [SYSTEM_STATUS_TABLE + 6] && 		//not process running on core 2
			[PROCESS_TABLE + 16*procId + 6] == 0 &&		//not swapped
			(R4 == CREATED || R4 == READY)
		) then
			scheduleProc = procId;
			break;
		endif;

		procId = (procId + 1) % MAX_PROC_NUM;
		procCount = procCount + 1;
	endwhile;

else
	scheduleProc = IDLE2_PROCESS;

	//if paging status or logout status is set, then schedule idle2
	if([SYSTEM_STATUS_TABLE + 5] != 0 || [SYSTEM_STATUS_TABLE + 7] == 1) then
		goto schedule_proc;
	endif;

	//available processes are 3-14 [12 count]
	alias procId R2;
	//currentPID-3+1
	procId = ((currentPID - 2) % 12) + 3;
	alias procCount R3;
	procCount = 0;
	while (procCount < 12) do

		R4 = [PROCESS_TABLE + 16*procId + 4];
		if (
			procId != IDLE2_PROCESS && 
			procId != [SYSTEM_STATUS_TABLE + 1] && 		//not process running on core 1
			[PROCESS_TABLE + 16*procId + 6] == 0 &&		//not swapped
			(R4 == CREATED || R4 == READY)
		) then
				scheduleProc = procId;
				break;
		endif;

		procId = ((procId- 2) % 12) + 3;
		procCount = procCount + 1;
	endwhile;
endif;


schedule_proc:

[SYSTEM_STATUS_TABLE + 5*CORE + 1] = scheduleProc;

alias newProcessTable R2;
newProcessTable = PROCESS_TABLE + 16*scheduleProc;

PTBR = [newProcessTable + 14];
PTLR = [newProcessTable + 15];

//if newly created then no user context to restore
if([newProcessTable + 4] == CREATED) then
	//BP is saved to the beginning of the kernel stack by Fork call at process creation
	BP = [[newProcessTable + 11]*PAGE_SIZE];
	[newProcessTable + 4] = RUNNING;
	[newProcessTable + 9] = 0;				//mode flag
	multipush(R2);
	R1 = RELEASE_LOCK;
	R2 = SCHED_LOCK;
	call ACCESS_CONTROL;
	multipop(R2);
	SP = [newProcessTable + 13];
	ireturn;
endif;

[newProcessTable + 4] = RUNNING;

multipush(R2);
R1 = RELEASE_LOCK;
R2 = SCHED_LOCK;
call ACCESS_CONTROL;
multipop(R2);

SP =  [newProcessTable+11] * PAGE_SIZE + [newProcessTable+12];
multipop(BP);
return;